dialect "mvel"

import java.util.List;
import java.lang.Integer;
import java.lang.Long;
import java.time.LocalDateTime;
import org.slf4j.Logger;

import com.example.siemcenter.common.models.Device;
import com.example.siemcenter.common.repositories.DeviceRepository;
import com.example.siemcenter.common.models.FactStatus;
import com.example.siemcenter.alarms.models.Alarm;
import com.example.siemcenter.alarms.repositories.AlarmRepository;
import com.example.siemcenter.logs.models.Log;
import com.example.siemcenter.logs.models.LogType;
import com.example.siemcenter.users.models.User;
import com.example.siemcenter.users.drools.UserTrait;
import com.example.siemcenter.users.models.RiskCategory;
import com.example.siemcenter.users.repositories.UserRepository;

global Logger logger;
global DeviceRepository deviceRepository;
global AlarmRepository alarmRepository;
global UserRepository userRepository;
global Integer deviceNumber;

declare ProcessedAlarm
    ruleTriggered: String
    id: Long
end

declare trait AlarmUser
end


// TODO: On at least 3 different parts of Information System
rule "#23 Fetch all users that have triggered at least 6 alarms in past 6 months" salience 1
    when
        Alarm()
        $user: User() from userRepository.findAll()
        Number(intValue >= 6) from accumulate (
            $alarm: Alarm(relatedUsers contains $user)
            over window:time(180d), count($alarm)
        )
        not ( UserTrait(username == $user.getUsername(), ruleTriggered == "#23") )
    then
        logger.info("#23 user inserted in WM: " + $user.toString());
        UserTrait $trait = new UserTrait($user);
        $trait.setRuleTriggered("#23");
        insert($trait);
end

query fetchUsersForReportCreation()
    $user: UserTrait()
end

rule "#24 Failed attempts to login from N different devices in a time span of 12 hours" salience 5
    when
        $log: Log(message.toLowerCase() contains "login"
                || message.toLowerCase() contains "log in"
                || message.toLowerCase() contains "log-in",
                logType == LogType.ERROR,
                $user: user,
                $ip: device.getIpAddress()
        )
        Number(intValue >= deviceNumber.intValue()) from accumulate (
            $l: Log(message.toLowerCase() contains "login"
                    || message.toLowerCase() contains "log in"
                    || message.toLowerCase() contains "log-in",
                    user == $user,
                    device.getIpAddress() != $ip,
                    logType == LogType.ERROR
            ) over window:time(12h), count($l)
        )
        not ( UserTrait(username == $user.getUsername(), ruleTriggered == "#24") )
    then
        logger.info("#24 user inserted in WM: " + $user.toString());
        UserTrait $trait = new UserTrait($user);
        $trait.setRuleTriggered("#24");
        insert($trait);
end

// TODO: After some time data in memory can become stale so we'll never check again if user has triggered an alarm in past 10 days
rule "#25 Users that triggered at least 10 alarms in past 10 days" salience 5
    when
        $alarm: Alarm()
        $user: User() from userRepository.findAllUsers()
        Number(intValue >= 10) from accumulate (
            $a: Alarm(relatedUsers contains $user)
            over window:time(10d), count($a)
        )
        not ( ProcessedAlarm(ruleTriggered == "#25", id.equals($alarm.getId()) ) )
        not ( UserTrait(username == $user.getUsername(), ruleTriggered == "#25") )
    then
        logger.info("#25 user inserted in WM: " + $user.toString());
        UserTrait $trait = new UserTrait($user);
        $trait.setRuleTriggered("#25");
        insert($trait);
        insert(new ProcessedAlarm("#25", $alarm.getId()));
end

rule "#26 Parts of system that generated five alarms while users with HIGH or EXTREME risk factors worked on them" salience 5
    when
        $alarm: Alarm()
    then
end